# A small example World for hubot-adventure.
#
# To load:
#
# ```
# hubot adventure world create Gnomicorn
# hubot adventure load https://github.com/smashwilson/hubot-adventure/examples/gnomicorn.gn
# ```

letgame switchOn = false
letgame pebbleCount = 5

# -- Room 1 --

let room1 = defineRoom("Room 1", "The first room")

room1.command("look", {
  let s = "
    This is the first room. There is a switch on the wall.
  ".compose()

  if {switchOn} then {
    s << "The switch is currently turned on."
  } else {
    s << "At the moment, the switch is turned off."
  }

  s.sep()

  s << "There is a door to the east."

  say(s)
})

let switch = room1.noun("switch")
switch.verb("look", {
  say(if {switchOn} then {
    "It's your typical dual-state lightswitch. It is currently in the 'on' position."
  } else {
    "The lightswitch is currently in the 'off' position."
  })
})

switch.verb(list("toggle", "flip", "activate", "use"), {
  switchOn = !switchOn
  say(if {switchOn} then {
    "You flip the switch 'on' and an overhead lamp illuminates."
  } else {
    "You flip the switch 'off'. The lamp turns off. It isn't actually that dark, though."
  })
})

# -- Room 2 --

let room2 = defineRoom("Room 2", "The second room")

linkRooms(room1, "east", room2)

room2.command("look", {
  let s = "
    This is the second room.
  ".compose()

  if {pebbleCount == 1} then {
    s << "There is a single pebble here."
  }
  if {pebbleCount > 1} then {
    s << "There is a pile of "
    s << pebbleCount.string()
    s << " pebbles here."
  }

  s.sep()

  s << "There is a door to the west."
})

let pebblePile = room2.noun("pebble")
pebblePile.verb("take", {
  if {pebbleCount > 0} then {
    pebbleCount = pebbleCount - 1
    say("You pick up a pebble, place it in your pocket, and then immediately forget about it forever.")
  } else {
    say("There are no more pebbles to take.")
  }
})
